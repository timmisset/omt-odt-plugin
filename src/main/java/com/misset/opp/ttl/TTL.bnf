// Based on RDF 1.1 Turtle
// https://www.w3.org/TR/turtle/

{
  parserClass="org.intellij.sdk.language.parser.TTLParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="TTL"
  psiImplClassSuffix="Impl"
  psiPackage="com.misset.opp.ttl.psi"
  psiImplPackage="com.misset.opp.ttl.psi.impl"

  elementTypeHolderClass="com.misset.opp.ttl.psi.TTLTypes"
  elementTypeClass="com.misset.opp.ttl.psi.TTLElementType"
  tokenTypeClass="com.misset.opp.ttl.psi.TTLTokenType"
}

TTLFile                 ::= declaredBaseUri? declaredImportUri* statement*
declaredBaseUri         ::= BASE_URI
declaredImportUri       ::= IMPORT_URI
statement               ::=	directive | triples DOT
directive	            ::=	declarePrefix | base | sparqlPrefix | sparqlBase
declarePrefix           ::=	ATPREFIX prefix IRIREF DOT {
    implements="com.misset.opp.ttl.psi.extend.TTLDeclarePrefix"
    mixin="com.misset.opp.ttl.psi.impl.iri.TTLDeclarePrefixAbstract"
}
base	                ::=	ATBASE IRIREF DOT                                   { extends=directive }
sparqlBase	            ::=	BASE_LEADING IRIREF                                 { extends=directive }
sparqlPrefix	        ::=	PREFIX_LEADING prefix IRIREF               { extends=directive }
triples	                ::=	subject predicateObjectList | blankNodePropertyList predicateObjectList?
// predicateObject is not part of the TTL literature but makes it easier to navigate the Psi Tree
predicateObject         ::= verb objectList
predicateObjectList	    ::=	predicateObject (SEMICOLON (predicateObject)?)*
objectList	            ::=	object (COMMA object)*
verb	                ::=	predicate | A
subject	                ::=	iri | blankNode | collection {
    extends="com.misset.opp.ttl.psi.impl.spo.TTLSubjectAbstract"
    implements="com.intellij.psi.StubBasedPsiElement<com.misset.opp.ttl.stubs.subject.TTLSubjectStub>, com.misset.opp.ttl.psi.extend.TTLQualifiedIriResolver"
    elementTypeClass="com.misset.opp.ttl.stubs.subject.TTLSubjectStubElementType"
}
predicate	            ::=	iri { extends=verb }
object	                ::=	iri | blankNode | collection | blankNodePropertyList | literal {
    extends="com.misset.opp.ttl.psi.impl.spo.TTLObjectAbstract"
    implements="com.misset.opp.ttl.psi.TTLStubBasedObject"
    elementTypeClass="com.misset.opp.ttl.stubs.object.TTLObjectStubElementType"
}
literal	                ::=	rdfLiteral | numericLiteral | booleanLiteral
blankNodePropertyList	::=	BRACKET_OPEN predicateObjectList BRACKET_CLOSE
collection	            ::=	PARENTHESES_OPEN object* PARENTHESES_CLOSE
numericLiteral	        ::=	INTEGER | DECIMAL | DOUBLE { extends=literal }
rdfLiteral	            ::=	string (LANGTAG | DATATYPE_LEADING iri)? { extends=literal }
booleanLiteral	        ::=	TRUE | FALSE { extends=literal }
string	                ::=	STRING_LITERAL_QUOTE | STRING_LITERAL_SINGLE_QUOTE | STRING_LITERAL_LONG_SINGLE_QUOTE | STRING_LITERAL_LONG_QUOTE
iri	                    ::=	iriReference | curie { implements="com.misset.opp.ttl.psi.extend.TTLQualifiedIriResolver" }
curie	                ::= prefix localname {
                                    mixin="com.misset.opp.ttl.psi.impl.iri.TTLCurieAbstract"
                                    extends=iri
                                    }
prefix                  ::= PNAME_NS {
                                        mixin="com.misset.opp.ttl.psi.impl.iri.TTLPrefixAbstract"
                                        implements="com.intellij.psi.PsiNamedElement"
                                      }
localname               ::= PN_LOCAL {
                                     mixin="com.misset.opp.ttl.psi.impl.iri.TTLLocalnameAbstract"
                                     implements="com.misset.opp.ttl.psi.extend.TTLQualifiedIriResolver"
                                     }
blankNode	            ::=	BLANK_NODE_LABEL | ANON
iriReference            ::= IRIREF {
                                       mixin="com.misset.opp.ttl.psi.impl.iri.TTLIriReferenceAbstract"
                                       extends=iri
                                   }
