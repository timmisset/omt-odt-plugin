{
  parserClass="org.intellij.sdk.language.parser.ODTParser"

  extends="com.misset.opp.odt.psi.impl.ODTASTWrapperPsiElement"
  implements="com.misset.opp.odt.psi.ODTPsiElement"

  psiClassPrefix="ODT"
  psiImplClassSuffix="Impl"
  psiPackage="com.misset.opp.odt.psi"
  psiImplPackage="com.misset.opp.odt.psi.impl"

  elementTypeHolderClass="com.misset.opp.odt.psi.ODTTypes"
  elementTypeClass="com.misset.opp.odt.psi.ODTElementType"
  tokenTypeClass="com.misset.opp.odt.psi.ODTTokenType"
}

ODTFile ::= script

namespacePrefix::=                          SYMBOL COLON {
    extends="com.misset.opp.odt.psi.impl.prefix.ODTBaseNamespacePrefix"
    implements="com.intellij.psi.PsiNamedElement"
}

constantValue ::=                          STRING+ | INTEGER | DECIMAL | NULL | BOOLEAN | TYPED_VALUE | PRIMITIVE | interpolatedString {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableConstantValueStep"
}
interpolatedString ::=                      INTERPOLATED_STRING_START interpolatedStringContent* INTERPOLATED_STRING_END {
    extends=constantValue
}
interpolatedStringContent ::=               STRING | interpolation
interpolation ::=                           INTERPOLATION_START script? INTERPOLATION_END
// a variable is declared using VAR and one or more variable names. Optionally an assignment is added
// the assignment is deconstructed to the entire set of variables that is declared. i.e. VAR $a, $b = [1, 2]. $a == 1, $b == 2
// or can be VAR $a = 1, $b = 2 etc.
declareVariable ::=                         DECLARE_VAR (variableAssignment | variable) (COMMA (variableAssignment | variable))* {
    extends=scriptContent
}
// a variable assignment is $var = 1234
variableAssignment::=                       variable (COMMA variable)* EQUALS variableValue {
                                                                                                extends=scriptContent
                                                                                            }
// a variable can be assigned with anything
variableValue ::=                           query | commandCall
variable ::=                                VARIABLE_NAME {
    extends="com.misset.opp.odt.psi.impl.variable.ODTBaseVariable"
    implements="com.misset.opp.odt.psi.impl.variable.ODTVariableWrapper"
    }

// SCRIPT
// an ODT (script) block is always started by a Yaml scalar node
// it can start on the same line and be restricted to that line (not required) or it can be started by the
// literal indentifier, the "|" in Yaml. In which case it should be followed by an empty line and and 
script ::=                                  scriptLine+
// a scriptline is contained by the addition of the semicolon at the end
scriptLine ::=                              ((scriptContent SEMICOLON*) | commandBlock | logicalBlock | defineQueryStatement | defineCommandStatement)
// the contents of a script, contained by a scriptLine or Scalar
scriptContent ::=                           declareVariable | variableAssignment | definePrefix | commandCall | assignmentStatement |
                                            addToCollection | removeFromCollection | returnStatement | queryStatement

commandBlock ::=                            CURLY_OPEN script? CURLY_CLOSED
queryStatement ::=                          query { extends=scriptContent }

// DEFINED MEMBERS
// ODT Members are Operator and Commands, both can be created by a DEFINE statement
// there is also a collection of BuiltIn operators and commands which are included
// and finally, all runnable OMT modelitems are available as Commands (Activity, Procedure) or Operator (StandAloneQuery)
// only the Defined members are part of set of the grammar parser
defineName ::=                              SYMBOL {
    extends="com.misset.opp.odt.psi.impl.callable.ODTResolvableDefineName"
    implements="com.misset.opp.odt.psi.impl.resolvable.ODTResolvable"
}
defineQueryStatement ::=                    DEFINE_START DEFINE_QUERY defineName defineParam? LAMBDA query SEMICOLON*  { extends="com.misset.opp.odt.psi.impl.callable.ODTBaseDefineQueryStatement"}
defineCommandStatement ::=                  DEFINE_START DEFINE_COMMAND defineName defineParam? LAMBDA commandBlock { extends="com.misset.opp.odt.psi.impl.callable.ODTBaseDefineCommandStatement"}
definePrefix ::=                            PREFIX_DEFINE_START namespacePrefix IRI {
    extends=scriptContent
    mixin="com.misset.opp.odt.psi.impl.prefix.ODTBaseDefinePrefix"
}
defineParam ::=                             PARENTHESES_OPEN (variable (COMMA variable)*)? PARENTHESES_CLOSE
callName ::=                                SYMBOL | BOOLEAN_OPERATOR
// MEMBER CALLS
// A call to either an Operator or a Command. In case of the latter, it's always prefixed with @
operatorCall ::=                            callName flagSignature? signature? {
    extends=queryStep
    implements="com.misset.opp.odt.psi.impl.resolvable.call.ODTCall"
    mixin="com.misset.opp.odt.psi.impl.resolvable.call.ODTOperatorCallImpl"
}
commandCall ::=                             AT callName flagSignature? signature? {
    extends=scriptContent
    mixin="com.misset.opp.odt.psi.impl.resolvable.call.ODTCommandCallImpl"
    implements="com.misset.opp.odt.psi.impl.resolvable.call.ODTCall"
}
signature ::=                               PARENTHESES_OPEN (signatureArgument (COMMA signatureArgument)*)? PARENTHESES_CLOSE
signatureArgument ::=                       resolvableValue | commandBlock {
    implements="com.misset.opp.odt.psi.impl.resolvable.ODTResolvable"
    extends="com.misset.opp.odt.psi.impl.resolvable.call.ODTResolvableSignatureArgument"
}
flagSignature ::=                           TAG
resolvableValue ::=                         query | commandCall {
    implements="com.misset.opp.odt.psi.impl.resolvable.ODTResolvable"
    extends="com.misset.opp.odt.psi.impl.resolvable.ODTResolvableValueBase"
}
logicalBlock ::=                            ifBlock commandBlock? (ELSE_OPERATOR ifBlock commandBlock?)* elseBlock?
ifBlock ::=                                 IF_OPERATOR (query | (PARENTHESES_OPEN query PARENTHESES_CLOSE))
elseBlock ::=                               ELSE_OPERATOR commandBlock
returnStatement ::=                         RETURN_OPERATOR resolvableValue? { extends=scriptContent }
chooseBlock ::=                             CHOOSE_OPERATOR 
                                                whenPath*
                                                otherwisePath?
                                                endPath? {
                                                    extends=queryStep
                                                    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.choose.ODTResolvableChooseBlockStep"
                                                    implements="com.misset.opp.odt.psi.impl.resolvable.ODTResolvable"
                                                }
whenPath ::=                                WHEN_OPERATOR query? LAMBDA? query? {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.choose.ODTResolvableWhenPathStep"
    implements="com.misset.opp.odt.psi.impl.resolvable.ODTResolvable"
}
otherwisePath ::=                           OTHERWISE_OPERATOR LAMBDA? query? {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.choose.ODTResolvableOtherwisePathStep"
    implements="com.misset.opp.odt.psi.impl.resolvable.ODTResolvable"
}
endPath ::=                                 END_OPERATOR 
// QUERIES
// A query is the superclass of different variants of a query.
// it is used to break the array into meaningful subsections based on detected tokens
// for example, the queryArray will break the array into subsections by the delimiter '|' for the array
//
// with regular bnf -> psi generation, the query would encapsulate the queryPath, ... items
// by using the extension mechanism, it only becomes their superclass
query     ::=                               queryPath | queryArray | booleanStatement | equationStatement {
    extends="com.misset.opp.odt.psi.impl.resolvable.query.ODTResolvableQuery"
    implements="com.misset.opp.odt.psi.impl.resolvable.ODTResolvable"
}
// The queryPath is the base, eventually all queries will descent into a queryPath even for single steps
// For example, a queryArray can consist of 'a' | 'b' in which both 'a' and 'b' are encapsulated
// into a queryPath and subsequently a queryStep
// The queryPath is constructed using the Forward slash as delimiter a contains as many steps as possible
queryPath ::=                               FORWARD_SLASH? queryOperationStep (FORWARD_SLASH queryOperationStep)* {
    extends=query
    mixin="com.misset.opp.odt.psi.impl.resolvable.query.ODTResolvableQueryPath"
}
// The queryArray uses the PIPE symbol to split the array where both can be resolved separately
queryArray ::=                              query (PIPE query)+ {
    extends=query
    mixin="com.misset.opp.odt.psi.impl.resolvable.query.ODTResolvableQueryArray"
}
// A booleanStatement works the same as the queryArray but using a BOOLEAN_OPERATOR
// When resolving, it should be checked that all parts of the query are resolved to boolean types
booleanStatement ::=                        query (BOOLEAN_OPERATOR query)+ {
    extends=query
    mixin="com.misset.opp.odt.psi.impl.resolvable.query.ODTResolvableBooleanStatement"
}
// The equationStatement uses a conditional operator (like ==, >=, <=) to split the statement
// it only allows a left-hand and right-hand query, both should be resolved to boolean type
equationStatement ::=                       query CONDITIONAL_OPERATOR query {
    extends=query
    mixin="com.misset.opp.odt.psi.impl.resolvable.query.ODTResolvableEquationStatement"
}
// The negated statement detects the usage of the NOT operator
// This can be used as '.. / NOT IN (...)' OR '.. / NOT'. In the former the negation is based on the subsequent
// query whereas the latter it's based on the preceding query
//
// A negated stap should not encapsulate a booleanStatement
// x == 0 AND NOT y == 1 should resolve in a booleanStatement where the latter contains the negation
negatedStep ::=                             NOT_OPERATOR (equationStatement | queryPath | queryArray)?  {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableNegatedStep"
}

queryOperationStep ::= (queryStep stepDecorator? queryFilter*) | queryFilter {
    extends="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableQueryOperationStep"
}
queryStep ::=
        queryReverseStep | constantValue | variableStep | curieElement | operatorCall | iriStep | identifierStep | schemalessIriStep |
         chooseBlock | negatedStep | subQuery {
     extends="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableQueryStepBase"
     implements="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableQueryStep"
 }
variableStep ::= variable {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableVariableStep"
}
iriStep ::= IRI {
    extends=queryStep
     mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.traverse.ODTResolvableIriStep"
}
identifierStep ::= DOT {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableIdentifierStep"
}
schemalessIriStep ::= SCHEMALESS_IRI {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.traverse.ODTResolvableSchemalessIriStep"
}
curieElement ::=                            namespacePrefix (SYMBOL | PRIMITIVE) {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.traverse.ODTResolvableCurieElementStep"
}
queryReverseStep ::=                        CARET queryStep {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.traverse.ODTResolvableQueryReverseStep"
}
// Decorators for the query steps, can be added to individual steps or subqueries
// QUESTION_MARK ?  == 0 or 1
// PLUS +           == 1 or more
// ASTERIX          == 0 or more
stepDecorator ::=                           ASTERIX | PLUS | QUESTION_MARK
// a query filter is contained by brackets [] and should resolve to boolean
queryFilter ::=                             BRACKET_OPEN query? BRACKET_CLOSED

// the subQuery encapsulated a query by PARENTHESES, it can be resolved as-is, for example in an equation
// or be cast to a traverse-like behavior using the ASTERIX and PLUS appendixes
subQuery ::=                                PARENTHESES_OPEN query PARENTHESES_CLOSE {
    extends=queryStep
    mixin="com.misset.opp.odt.psi.impl.resolvable.queryStep.ODTResolvableSubQueryStep"
}

// the assignment statement is used to assign a value to a left-hand target, like a variable or a resolved resource iri
// addTo and removeFrom are similar which will add/remove the item to/from the collection
// these are not used defined query statement but in command blocks
assignmentStatement ::=                     query EQUALS resolvableValue { extends=scriptContent }
addToCollection ::=                         query ADD resolvableValue { extends=scriptContent }
removeFromCollection ::=                    query REMOVE resolvableValue { extends=scriptContent }
