plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.3.0'
    id 'jacoco'
}

intellij {
    version = '2021.3'
    type = 'IU'
    plugins = [
            'java',
            'JavaScriptLanguage',
            'AngularJS',
            'org.jetbrains.plugins.yaml'
    ]
}

group 'com.misset'
version '5.4.1'

sourceCompatibility = 11
sourceSets.main.java.srcDirs 'src/main/gen'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.4.6'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.2.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
    implementation group: 'org.apache.jena', name: 'jena', version: '3.17.0', ext: 'pom' exclude group: 'org.slf4j'
    implementation group: 'org.apache.jena', name: 'jena-core', version: '3.17.0' exclude group: 'org.slf4j'
}
test {
    useJUnitPlatform {

    }
    systemProperty "idea.home.path", "C:/Program Files/JetBrains/IntelliJ IDEA 2021.3"
}
jacocoTestReport {
    // due to the unpredictability of the IntelliJ Platform, unit tests should not be considered part of the coverage
    // only the fixture tests should be used as advised by the JetBrains
    dependsOn test // tests are required to run before generating the report

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/gen/**')
        }))
    }
}
buildSearchableOptions.enabled = false
// See https://github.com/JetBrains/gradle-intellij-plugin/

patchPluginXml {
    sinceBuild = '212'

    changeNotes = """
        <h1>Update 5.4.1</h1>
        Fixed #25: <a href="https://github.com/timmisset/omt-odt-plugin/issues/25">Sorting OMTPrefixes by frequency</a><br>
        Fixed #27: <a href="https://github.com/timmisset/omt-odt-plugin/issues/27">No error on multiple missing imports</a><br>
        Fixed #30: <a href="https://github.com/timmisset/omt-odt-plugin/issues/30">ODTResolvableQualifiedUriStepDocumentationUtil nullpointer</a><br>
        Re-added #29: <a href="https://github.com/timmisset/omt-odt-plugin/issues/29">Commenter</a><br>
        Create a line comment using ctrl+slash or block comment using ctrl+shift+slash<br>
        <br>
        <h1>Update 5.4</h1>
        ODT Color settings is replaced. Color coding based ontology type (owl:Class, instance etc) was too heavy for
        the highlighting parser and is removed. Replaced with highlighting settings that closely resemble the Java color settings.<br>
        <br>
        Added #21: <a href="https://github.com/timmisset/omt-odt-plugin/issues/21">Loadable</a><br>
        First implementation of !Loadable is supported:<br>
        - Shows suggestion for json files to import, automatically resolves to relative path<br>
        - Binds to Json, meaning if the Json is moved the path reference is automatically updated<br>
        - Validates the paths are to existing .json files<br>
        <br>
        Added #18: <a href="https://github.com/timmisset/omt-odt-plugin/issues/18">\$activityConfig global variable</a><br>
        Adapted #20: <a href="https://github.com/timmisset/omt-odt-plugin/issues/20">Declared from interface</a><br>
        Fixed #22: <a href="https://github.com/timmisset/omt-odt-plugin/issues/22">Nullpointer in listOrderedSuperClasses</a><br>
        Fixed #23: <a href="https://github.com/timmisset/omt-odt-plugin/issues/23">New line indentation</a><br>
        """
}
tasks {
    runIde {
        jvmArgs("-Xmx2000m")
    }
    runPluginVerifier {
        ideVersions = ["IC-2021.1", "IC-2021.2"]
    }


}
publishPlugin {
    token = System.getenv().getOrDefault("ORG_GRADLE_PROJECT_intellijPublishToken", "")
}
